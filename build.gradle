plugins {
	id "dev.architectury.loom" version "1.7-SNAPSHOT"
	id "architectury-plugin" version "3.4-SNAPSHOT"
	id "com.modrinth.minotaur"
}

base {
	archivesName = project.archives_base_name
}

var String loader = loom.platform.get().displayName.toLowerCase()
var String mcVersion = stonecutter.current.version

var isFabric = loader == "fabric"
var isNeoForge = loader == "neoforge"
var isForge = loader == "forge"

// The current stonecutter version (0.4) has an issue with groovy gradle which makes
// stonecutter.configureEach { stonecutter.const(...) } not work correctly inside stonecutter.gradle.
// Putting it here fixes the problem.
stonecutter.const("fabric", isFabric)
stonecutter.const("neoforge", isNeoForge)
stonecutter.const("forge", isForge)

static def buildVersionString(String modVersion, String releaseType, String mcVersion, String loader) {
	var semver = modVersion
	var isPrerelease = releaseType != "release"
	if (isPrerelease) semver += "-" + releaseType
	semver += "+" + mcVersion + "-" + loader
	if (isPrerelease) semver += ".rev." + 'git rev-parse --short HEAD'.execute().text.trim()
	return semver
}

version = buildVersionString(project.mod_version, project.release_type.toLowerCase(), mcVersion, loader)
group = project.maven_group

architectury {
	minecraft = mcVersion

	platformSetupLoomIde()
	if (isFabric)
		fabric()
	if (isForge)
		forge()
	if (isNeoForge)
		neoForge()
}

repositories {
	maven {
		name = "TerraformersMC"
		url = "https://maven.terraformersmc.com/releases/"
	}
	maven {
		name = "Modrinth"
		url = "https://api.modrinth.com/maven"
		content {
			includeGroup "maven.modrinth"
		}
	}
	maven {
		name = "Xander"
		url = "https://maven.isxander.dev/releases"
	}
	maven {
		name = 'NeoForged'
		url = 'https://maven.neoforged.net/releases'
	}
	maven { url "https://maven.quiltmc.org/repository/release" }
}

loom {
	accessWidenerPath = project.rootProject.file("src/main/resources/betterclouds.accesswidener")

	runConfigs.configureEach {
		ideConfigGenerated true
		runDir "../../run"
	}

	if (isForge) forge {
		convertAccessWideners.set(true)
		mixinConfigs(
			"betterclouds.mixins.json",
			"betterclouds.optional.mixins.json",
		)
	}
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:$mcVersion"
	mappings loom.layered {
		it.mappings("net.fabricmc:yarn:${property('yarn_mappings')}:v2")
		if (property('yarn_mappings_patch_neoforge_version') != "") {
			it.mappings("dev.architectury:yarn-mappings-patch-neoforge:${property('yarn_mappings_patch_neoforge_version')}")
		}
	}

	if (isFabric) {
		modImplementation "net.fabricmc:fabric-loader:${property('fabric_loader_version')}"
		modImplementation "net.fabricmc.fabric-api:fabric-api:${property('fabric_version')}"

		modImplementation "com.terraformersmc:modmenu:${property('modmenu_version')}"
		modImplementation "dev.isxander:yet-another-config-lib:${property('yacl_version')}-fabric"

		modCompileOnly "maven.modrinth:sodium-extra:${property('sodium_extra_version')}"
		modCompileOnly "maven.modrinth:iris:${property('iris_version')}"
	}

	if (isNeoForge || isForge) {
		if (isNeoForge)
			neoForge "net.neoforged:neoforge:${property('neoforge_loader_version')}"
		if (isForge)
			forge "net.minecraftforge:forge:${property('forge_loader_version')}"

		modImplementation "dev.isxander:yet-another-config-lib:${property('yacl_version')}-neoforge"

		// TODO: Need a proper solution!! Should be included with yacl
		forgeRuntimeLibrary(runtimeOnly("org.quiltmc.parsers:json:0.2.1"))
		forgeRuntimeLibrary(runtimeOnly("org.quiltmc.parsers:gson:0.2.1"))

		modCompileOnly "maven.modrinth:rubidium-extra:0.5.7e+mc1.21-build.129"
		modCompileOnly "maven.modrinth:oculus:1.20.1-1.7.0"

		// Because yarn mappings are used the mods can't be loaded from the 'mods' directory at runtime.
		// It causes NoClassDefFoundErrors due to the different mappings.
		// This is a workaround that uses 'modLocalRuntime' to remap the mods found in the 'mods-forge' directory.
		// It requires a project reload when the mods change.
		def localModsDir = project.rootProject.file('./run/mods-forge/')
		if (localModsDir.exists() && localModsDir.isDirectory()) {
			def localMods = localModsDir
					.listFiles({ file -> file.isFile() && file.name.endsWith('.jar') } as FileFilter)
			localMods.each { mod ->
				dependencies {
					modLocalRuntime files(mod)
				}
			}
		}
	}

	compileOnly project.rootProject.files("libraries/DistantHorizonsApi-3.0.0-nightly-377f7d23.jar")
}

processResources {
	var props = [
		version: project.version,
		mc_version: findProperty("minecraft_version_range")
	]

	props.forEach(inputs::property)

	filesMatching("fabric.mod.json") {
		expand props
	}

	filesMatching("META-INF/neoforge.mods.toml") {
		expand props
	}

	exclude("assets/**/*.ase")
	exclude("assets/**/*.xcf")
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = findProperty('java_version').toString().toInteger()
}

remapJar {
	if (isNeoForge) {
		atAccessWideners.add("betterclouds.accesswidener")
	}
}

stonecutter {
	var j21 = eval(mcVersion, ">=1.20.6")
	java {
		withSourcesJar()
		sourceCompatibility = j21 ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
		targetCompatibility = j21 ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
	}
}

jar {
	from(project.rootProject.file("LICENSE").path) {
		rename { "${it}_${project.archives_base_name}"}
	}
}

modrinth  { // Make sure it runs after build!
	def secrets = new Properties()

	project.rootProject.file("secrets.properties").withInputStream {
		stream -> secrets.load(stream)
	}


	token.set(secrets.MODRINTH as String)
	projectId = '5srFLIaK'
	versionNumber = "${property('mod_version')}+${mcVersion}-${property('release_type').substring(0, 1)}" // Will fail if Modrinth has this version already
	versionName = "${property('mod_version')} for ${mcVersion} ${property('release_type')}"
	versionType = "${property('release_type')}"
	// On fabric, use 'remapJar' instead of 'jar'
	uploadFile = remapJar  // This is the java jar task. If it can't find the jar, try 'jar.outputs.getFiles().asPath' in place of 'jar'
	gameVersions.add(mcVersion)
	loaders.add('fabric')
	dependencies { // A special DSL for creating dependencies
		// scope.type
		// The scope can be `required`, `optional`, `incompatible`, or `embedded`
		// The type can either be `project` or `version`
		required.project "fabric-api"
		required.version "yacl", "${property('yacl_version')}-fabric"
		optional.project "sodium"
		optional.project "iris"
		optional.project "modmenu"
		incompatible.project "vulkanmod"
	}
}
